import wio.io;
import wio.util;

local omni PhoneNumbers = {};
local omni Filepath = "save.txt";

local func AddNumber(omni pair)
{
	if(PhoneNumbers.Exists(pair.Key))
	{
		Println(pair.Key + " already exists!");
		return;
	}
	PhoneNumbers[pair.Key] = pair.Value;
}

local func AddNumber(omni name, omni number)
{
	if(PhoneNumbers.Exists(name))
	{
		Println(name + " already exists!");
		return;
	}
	PhoneNumbers[name] = number;
}

local func LoadNumbers()
{
	omni file = OpenFile(Filepath, OPEN_MODE_READ | OPEN_MODE_BINARY);
	if(!?file)
		return;
	omni content = Read(file);
	CloseFile(file);

	omni numbers = content.Split('\n');
	
	foreach(item in numbers)
	{
		omni NameNumber = item.Split('\t');
		omni pair = Pair(NameNumber[0], NameNumber[1]);
		AddNumber(pair);
	}
}

local func SaveNumbers()
{
	omni file = OpenFile(Filepath, OPEN_MODE_WRITE | OPEN_MODE_BINARY);

	foreach(item in PhoneNumbers)
	{
		file <- item.Key;
		file <- '\t';
		file <- item.Value;
		file <- '\n';
	}

	CloseFile(file);
}

local func AddMenu()
{
	Print("Name: ");
	omni name = Input();
	Print("Number: ");
	omni number = Input();

	AddNumber(name, number);
}

local func DisplayNumbers()
{
	Println(PhoneNumbers);
}

func Main()
{
	LoadNumbers();

	for(;;)
	{
		Println("1- Add Number");
		Println("2- Display Numbers");
		Println("3- Exit");
		Print("Choice: ");
		omni choice = Input().Int();

		if(choice == 1)
			AddMenu();
		else if(choice == 2)
			DisplayNumbers();
		else if(choice == 3)
		{
			SaveNumbers();
			return;
		}
	}
}

Main();